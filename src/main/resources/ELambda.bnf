{
  generate = [ token-accessors="yes" ]

  parserClass="com.github.lambdamix.elambda.ELambdaParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiVisitorName="ELambdaVisitor"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.lambdamix.elambda.psi"
  psiImplPackage="com.github.lambdamix.elambda.psi.impl"

  elementTypeHolderClass="com.github.lambdamix.elambda.ELambdaTypes"
  elementTypeClass="com.github.lambdamix.elambda.psi.ELambdaElementType"
  tokenTypeClass="com.github.lambdamix.elambda.psi.ELambdaTokenType"
  psiImplUtilClass="com.github.lambdamix.elambda.psi.impl.ELambdaPsiImplUtil"

  tokens = [
    KW_LAM       = "lam"
    KW_CASE      = "case"
    KW_DATA      = "data"
    KW_OF        = "of"
    KW_WHERE     = "where"
    PAR_LEFT     = "("
    PAR_RIGHT    = ")"
    CURLY_LEFT   = "{"
    CURLY_RIGHT  = "}"
    ARROW        = "->"
    SEMI         = ";"
    DOT          = "."
    COMMA        = ","
    OP_OR        = "|"
    OP_EQ        = "="
    ARROW        = "->"
  ]

  extends(".*_expr")=expr
}

elambdaFile ::= (type_definition | binding | COMMENT)*

type_definition ::= 'data' UID type_variable_defs_ '=' data_constructors_ ';' {
    methods=[
        getName
        typeName="/UID"
    ]
}
private type_variable_defs_ ::=  '(' ')' | '(' type_variable_def (','  type_variable_def)* ')'
private data_constructors_ ::= data_constructor ('|' data_constructor)*

data_constructor ::= UID top_types_ {
    methods=[
        getName
        ctrName="/UID"
    ]
}
private top_types_ ::= '('  ')' | '(' type (',' type)* ')'

type_variable_def ::= LID {
    methods=[
        getName
        varName="/LID"
    ]
}
type_variable ::= LID {
    extends=type
    methods=[
        getName
        getReference
        varName="/LID"
    ]
}
type_constructor ::= UID top_types_ {
    extends=type
    methods=[
        getName
        getReference
        ctrName="/UID"
    ]
}
remote_type_id ::= mod '.' UID
remote_type_constructor ::= remote_type_id top_types_ {
    extends=type
    methods=[
        getName
        getReference
        ctrName="/remote_type_id/UID"
        mod="/remote_type_id/mod"
    ]
}
type ::= remote_type_constructor | type_constructor | type_variable

mod ::= LID {
    methods=[
        getName
        getReference
        modName="/LID"
    ]
}
remote_ctr ::= mod '.' UID {
    methods=[
        ctrName="/UID"
    ]
}

binding ::= variable '=' expr ';'
variable ::= LID {
    methods=[
        getName
        varName="/LID"
    ]
}

expr ::= remote_constructor_expr | app_expr | remote_variable_expr | variable_expr | constructor_expr | lambda_expr | case_expr | paren_expr
private paren_expr ::= '(' expr ')'
app_expr ::= (remote_variable_expr | variable_expr | paren_expr) exprs_
private exprs_ ::= '('  ')' |  '(' expr (',' expr)* ')'
remote_variable_expr ::= mod '.' LID {
    methods=[
        getName
        getReference
        varName="/LID"
    ]
}
variable_expr ::= LID {
    methods=[
        getName
        getReference
        varName="/LID"
    ]
}
constructor_expr ::= UID exprs_ {
    methods=[
        getName
        getReference
        ctrName="/UID"
    ]
}
remote_constructor_expr ::= remote_ctr exprs_ {
    methods=[
        getName
        getReference
        ctrName="/remote_ctr/UID"
        mod="/remote_ctr/mod"
    ]
}
lambda_expr ::= 'lam' variables_ '->' expr
case_expr ::= 'case' expr 'of' '{' branch* '}'

branch ::= pattern '->' expr ';'
pattern ::= remotePattern | localPattern {
    methods=[getVariableList]
}

localPattern ::= UID variables_ {
    methods=[
        getName
        getReference
        patName="/UID"
    ]
}

remotePattern ::= remote_ctr variables_ {
    methods=[
        getName
        getReference
        patName="/remote_ctr/UID"
        mod="/remote_ctr/mod"
    ]
}


private variables_ ::= '('  ')' | '(' variable (',' variable)* ')'
